# Первые шаги

[[toc]]

## Благодарности

- Создателям отличного [гайда в Steam](https://steamcommunity.com/sharedfiles/filedetails/?id=690911411), на котором основано данное руководство:
  - [HenсoDesu](https://steamcommunity.com/id/hencodesu) - за создание гайда в Steam
  - [Владе](http://steamcommunity.com/id/cool_vlad) - отдельно за создание интересных модов и за помощь в дополнениии руководства в Steam
  - [Лене](http://steamcommunity.com/id/lena_sova) - отдельно за помощь в коментариях всем, кто столкнулся с затруднениями в Steam и создание полезных модификаций
- [Nikolas Khikkanov](https://vk.com/itsmlg) - за помощь в создании данного руководства

## Предпосылки

- Установленная игра "Бесконечное Лето".
- Текстовый редактор. Я рекомендую использовать [Visual Studio Code](https://code.visualstudio.com/) (+ [Ren'Py](https://marketplace.visualstudio.com/items?itemName=LuqueDaniel.languague-renpy)) или [Notepad++](https://notepad-plus-plus.org/), [Atom](https://atom.io/), как альтернативу

## Создание мода

Для начало нужно создать файл с расширением `.rpy`. Затем открыть его в текстовом редакторе, и следовать далее по руководству.

### Написание кода

Стиль и синтаксис Ren'Py похож на Python. Если знакомы с ним, то проблем возникнуть не должно.

Файлы состоят из блоков и команд в блоках, например:

```
<Блок>:
    <Команды>

<Блок>
```

::: warning
Команды в блоках должны везде иметь одинаковый отступ. Будь то 2 или 4 пробела.
В большинстве случаев **рекомендуется использовать 4 пробела**.
:::

В большинстве случаев в редакторе кода будет по-умолчанию выставлено значение отступов, поэтому рекомендуется использовать `TAB` для отступов.

### Объявление мода

::: danger
Шаги, приведенные ниже крайне важны. Без них мод не будет виден в списке модов в игре.
:::

Для того чтобы игра поняла, что файл (в котором строчки будут написаны) является модом, нужно написать следующее:

```renpy
init:
    $ mods["my_mod"] = "Мой мод"
```

Разберем более детально пример выше:

1. `my_mod` - название начального лейбла мода. При запуске мода, всегда первым начинает запускаться этот лейбл, указанный внутри командых скобок. Не будет виден пользователю. Должен быть уникальным, иначе возможны конфликты с модификациями, имеющими лейблы с таким же названием. **Можно использовать только латиницу!**
2. `Мой мод` - название мода, которое будет отображаться в списке модов. **Допускается использование кириллицы**.

Теперь, для того, чтобы игра поняла, где начинается мод, нужно добавить начальный лейбл:

```renpy{4-5}
init:
    $ mods["my_mod"] = "Мой мод"

label my_mod:
    "Реплика"
    # Далее будут идти команды
```

В коде выше:

1. Была объявлена метка (`label`), имя которой **обязательно должно соответствовать имени мода, указанного в квадратных скобках**. В нашем случае - `my_mod`.

## Написание сценария

Далее, в метке `my_mod` можно начинать писать сценарий. В нем используется специальный синтаксис Ren'Py, который мы разберем далее.

### Метки

Использование меток позволяет облегчить процесс создания мода, путем разделения сценария на части.

::: danger
Имена меток должны быть уникальными, дабы избежать конфликтов.
Рекомендуется использовать названия меток, начиная с названия мода, например, `my_mod_day1`.
:::

Общий синтаксис метки выглядит так:

```renpy
label <Название>:
    <Команды>
```

#### Переходы между лейблами

Есть два вида переходов:

1. **Прыжок (jump)**

```renpy
label my_mod_day1:
    "..."
    jump my_mod_day2 # Прижок на my_mod_day2

label my_mod_day2:
    "..."
```

В данном случае команды после "прыжка" будут пропущены игрой, например, фраза "действия после прыжка" из примера ниже не будет выведена на экран игрока. Любые другие команды, которые были написаны после прыжка также не сработают.

```renpy{4}
label my_mod_day1:
    "..."
    jump my_mod_day2
    "действия после прыжка"

label my_mod_day2:
    "..."
```

2. **Вызов (call)**

В этом случае, фраза из примера выше будет выведена на экран, после завершения действий из метки `my_mod_day2`.

```renpy{3}
label my_mod_day1:
    "..."
    call my_mod_day2 # Вызов второго лейбла
    "действия после прыжка"

label my_mod_day2:
    "..."
```

Закончить блок можно командой `return`. После неё мод будет завершен и будет показано главное меню игры.

В примере с `call`, если добавить `return`, то действия после "вызова" выполняться не будут, так как блок завершится в метке `my_mod_day2`.

```renpy{8}
label my_mod_day1:
    "..."
    call my_mod_day2
    "действия после прыжка"

label my_mod_day2:
    "..."
    return
```

### Реплики

Реплики персонажей имеют следующий общий вид: `<ID персонажа> "<Реплика>"`, где:

1. `ID персонажа` - название переменной, которой он был объявлен.
2. `Реплика` - текст, который будет говорить персонаж.

Список персонажей и их переменные представлены в таблице по ссылке: [Персонажи](../resources-tables.md#персонажи)

В примере ниже представлен пример реплик Мику и Ульяны, имена которых ещё не известны:

```renpy
label my_mod:
    mip "Привет, ты меня ещё не знаешь, но это я — Мику!"
    mip "В данный момент я у тебя отображаюсь как 'Пионерка'."
    usg "Привет, а я — Ульянка! Но у тебя я написана как \"Девочка\"."
```

В первом случае, имя пионера или пионерки будет сменено на "Пионер/Пионерка", сохраняя свой цвет. Во втором же случае имя персонажа будет сменено на "Девочка", сохраняя свой цвет.

#### Оформление текста

Примеры текстовых тегов:

1. **alpha**

Текстовый тег `alpha` отображает текст, расположенный между самим тегом и его закрывающим тегом, с указанной непрозрачностью. Непрозрачность должна иметь значение между 0.0 и 1.0, что соответствует полной прозрачности и полной непрозрачности, соответственно. Если значение начинается с префикса `+` (добавить на) или `-` (убавить на), непрозрачность будет изменена на указанное число, а не полностью заменена. Если значение начинается с префикса `*` (умножить на), непрозрачность будет умножена на это число.

```renpy
"{alpha=0.1}Этот текст едва читаем!{/alpha}"
"{alpha=-0.1}Этот текст на 10 процентов прозрачнее, чем текст по умолчанию.{/alpha}"
"{alpha=*0.5}Этот текст наполовину такой же непрозрачный, как и по умолчанию.{/alpha}"
```

2. **b** (bold)

Тег жирного шрифта выводит текст, расположенный между самим тегом и его закрывающим тегом, жирным шрифтом.

```renpy
"Пример {b}текста, выделенного жирным шрифтом{/b}."
```

3. **color**

Текстовый тег `color` отображает текст, расположенный между самим тегом и его закрывающим тегом, в указанном цвете. Цвет должен быть в формате #rgb, #rgba, #rrggbb или #rrggbbaa.

```renpy
"{color=#f00}Красный{/color}, {color=#00ff00}Зелёный{/color}, {color=#0000ffff}Синий{/color}"
```

4. **i** (italic)

Тег курсива отображает текст, расположенный между самим тегом и его закрывающим тегом, курсивом.

```renpy
mt "Добро пожаловать в {i}СОЛ «Совёнок»!{/i}."
```

5. **s** (strikethrough)

Тег зачеркивания проводит черту через текст, расположенный между тегом и его закрывающим тегом.

```renpy
mi "Я очень надеюсь, что {s}тебе понравилось{/s}."
```

6. **u** (underline)

Тег подчеркивания подчеркивает текст, расположенный между самим тегом и его закрывающим тегом.

```renpy
mi "Рада видеть {u}тебя{/u}!"
```

7. **w** (wait)

Тег ожидание – самозакрывающийся тег, который ждёт, пока читатель нажмёт на кнопку для продолжения. Если для тега задан аргумент в форме числа,то ожидание автоматически заканчивается, когда заданные секунды проходят.

```renpy
un "Я{w} очень{w=1.0} рада!"
```

Полный список текстовых тегов можно найти внутри [соответствующей статьи в документации Ren'Py](<http://ru.renpypedia.shoutwiki.com/wiki/%D0%A2%D0%B5%D0%BA%D1%81%D1%82_(Text)>)

## Фоны

### Бэкграунды[БГ]

Общий синтаксис для вывода фона: `scene <Название фона>` или `show <Название фона>`

::: tip Разница между scene и show
В первом случае, очищает слой и показывает изображение, во втором — просто показывает изображение.
Если нам необходимо сменить только фон, а спрайты не убирать, то можно прописать `show <Название фона>` и смениться только фон, оставив спрайты на своих местах.
:::

Например:

```renpy
scene bg bus_stop # Покажет нам автобусную остановку из времени Семена
scene bg int_bus # Внутри автобуса, привезшего нас в "Совенок"
scene bg ext_bus_night # Снаружи этого же автобуса, но уже ночью
```

Фоны из игры представлены в таблице по ссылке: [Изображения - Фоны](../resources-tables.md#фоны)

### Иллюстрации[ЦГ]

Общий синтаксис для вывода иллюстраций: `scene <Название арта>` или `show <Название арта>`

Например:

```renpy
scene cg d3_dv_scene_1 # Покажет арт с Алисой и Семёном на сцене, ночью.
scene cg d3_ussr_catched # Покажет лежащую на траве Ульянку.
scene cg d1_sl_dinner_0 # Славя и Семён ночью в столовой.
```

Все иллюстрации из игры представлены в таблице по ссылке: [Изображения - Иллюстрации](../resources-tables.md#cg)

## Спрайты

Вызывается спрайт с помощью: `show <Имя спрайта>`

Обычно спрайты состоят из слов, разделённых пробелом: `<имя персонажа> <эмоция> <одежда>`

::: tip
В оригинальной игре имя персонажа совпадает с ID говорящего из предыдущего раздела этого руководства
:::

Например:

```renpy
show dv angry pioneer # Злая Алиса
show un cry sport # Плачущая Лена в спортивной форме
```

Список всех спрайтов пока можно найти [здесь](https://drive.google.com/file/d/0B7BOgg028z7pbksxbFM3X1NOcXM/view).

::: warning
Если персонаж уже на экране, то повторное использование `show` будет менять эмоцию и/или одежду, если при повторном использовании у персонажа имеется "тэг".
Скажем, в команде `show un cry sport` — `un` выступает в качестве "тэга" персонажа, поэтому при смене одежды/эмоции не нужно будет убирать предыдущую картинку.
Точно также тэги работают и с изображениями по типу БГ, ЦГ, и прочих.
:::

Для того, чтобы убрать персонажа, воспользуйтесь: `hide <Имя персонажа>`

::: tip
Если вы хотите вслед за этим сменить декорации, то прописывать hide для каждого персонажа не обязательно, команда scene сама их уберет
:::

## Действия с изображениями

### Позиционирование

При простом использовании `show <Спрайт>` он будет появляться просто по центру, это можно исправить добавив в конец `at <Позиция>`: `show <Спрайт> at <Позиция>`.

Всего в игре 7 позиций где может находиться спрайт:

- `fleft`
- `left`
- `cleft`
- `center`
- `cright`
- `right`
- `fright`

![Пример расположения спрайтов](/images/examples/example_sprites_positioning.jpg)

На скриншоте выше были использованы следующие команды:

```renpy
scene bg ext_bus_night

show dv angry pioneer2 at left
show un smile pioneer at right
show mi cry_smile pioneer at cleft
show sl serious pioneer at cright
show mt rage panama pioneer at center
```

Так же можно показать персонажа ближе / дальше. Для этого можно воспользоваться атрибутом `close` / `far`.

![Пример использования атрибутов close / far](/images/examples/example_sprites_distances.jpg)

На скриншоте выше как раз были использованны эти атрибуты. Команды:

```renpy
scene bg ext_bus_night

show dv angry pioneer2 far at left
show un smile pioneer far at right
show mi cry_smile pioneer at cleft
show sl serious pioneer at cright
show mt rage panama pioneer close at center
```

### Анимации появления

Мы можем показать спрайты, bg и cg с некоторым замедлением, плавно.

- **Плавность**

`scene <Название> with <Атрибут>`

Атрибутов для плавности 6, они отличаются лишь своей скоростью:

1. `dspr` - 0,2 сек - Самый быстрый, на практике не особо заметен.
2. `dissolve_fast` - 0.5 сек.
3. `dissolve` - 1 сек - Средняя скорость, считаю оптимальной для смены cg на bg и наоборот и для эмоций спрайтов
4. `dissolve2` - 2 сек - Самая плавная картинка, считаю хорошим вариантом для переходов между локациями лагеря
5. `hell_dissolve` - 50 секунд - на практике не особо применим, но вдруг пригодится.
6. `dissolve_long` - 100 секунд.

Так же можно группировать несколько обьектов для одновременного начала эффекта:

```renpy{3}
scene bg ext_bus_night
show sl smile pioner far
with dissolve2
```

В приведенном примере мы попадем в ночной автобус и перед нами предстанет улыбающаяся Славя, и все это с задержкой в 2 секунды.

::: tip
Если все вышеприведённые примеры Вас не устраивают, можно добавить свой атрибут плавности. Для этого, пропишите его в блоке init.

Например,

```renpy
init:
    $ trisekundi = Dissolve(3.0)

label my_mod:
    scene bg ext_bus_night
    show sl smile pioner far
    with trisekundi
```

:::

- **Моргание**

Я более чем уверен, что вы видели эффект того, как иногда ГГ моргает или вовсе закрывает глаза. Мы можем реализовать подобную возможность!

- `show blink` - Закрыть глаза
- `show unblink` - Открыть глаза
- `show blinking` - Моргание

::: warning Будьте осторожны
blink и unblink это разные эффекты, они не могут отменить друг друга.
:::

Пример:

```renpy
show blink
<Ваши события>
hide blink
show unblink
```

::: tip
Так же к эффектам моргания можно применить атрибут плавности.
:::

### Добавление своих изображений

Вполне может быть, что по сюжету Вашего мода, может не хватить имеющихся БГ, ЦГ и спрайтов. В таком случае, мы добавим свои.

Для начала нам надо объявить изображение в блоке `init`. Делается это, с помощью объявления переменной `image`. Например, если есть изображение `city.jpg`, и оно находится в папке `game/mods/my_mod/city.jpg`, то объявить его можно вот так:

```renpy
init:
    image gorod = "mods/my_mod/city.jpg"
```

И использовать его:

```renpy{5}
init:
    image gorod = "mods/my_mod/city.jpg"

label my_mod:
    scene gorod with dissolve
```

::: tip Тэги к изображениям
Настоятельно рекомендую использовать тэги. В привидённом ниже примере слово `bg` выступает в виде тэга к изображению.
Если объявить несколько БГ с таким тегом, то при показе следующего он автоматически заменит предыдущий.
:::

```renpy
init:
    image bg city = "mods/my_mod/city.jpg"
```

Более детальную информацию про [ATL](https://www.renpy.org/doc/html/atl.html) смотрите в Продвинутом руководстве.

## Звуки

Ren'Py поддерживает воспроизведение музыки и звуковых эффектов, используя следующие форматы аудиофайлов: `Opus`, `Ogg Vorbis`, `mp3`, `wav`

### Добавление звуков, музыки, эмбиенса

`$ <Название аудиофайла> = <Путь к аудиофайлу>`

Добавление различных аудиофайлов в ваш мод происходит посредством команды, привидённой выше. Приведём пример использования.

```renpy{2}
init:
    $ city_ambience = "mods/sounds/city_ambience.mp3"

label mod_start:
    play ambience city_ambience # Проигрываем эмбиенс города на канале `ambience`
```

### Музыка

Для начала проигрывания музыки используется: `play music <Название>`, а для остановки - `stop music`.

Список музыки из игры представлен в таблице по ссылке: [Звуки - Музыка](../resources-tables.md#музыка)

### Окружение

`play ambience <Название>`

`stop ambience`

Звуки окружения из игры представлены в таблице по ссылке: [Звуки - Окружение](../resources-tables.md#окружение)

### Прочее

`play sound <Название>`

Прочие звуки из игры представлены в таблице по ссылке: [Звуки - Прочие звуки](../resources-tables.md#прочие-звуки)

### Плавный вход/выход музыки

Команды `fadein` и `fadeout` являются необязательными. `fadeout` дает время затухания для текущей проигрываемой музыки в секундах, в то время как `fadein` дает время, необходимое для плавного "входа" новой музыки.

Пример:

```renpy{2,4}
label my_mod:
    play music sabaton_rock fadein 4 # Воспроизведение музыки с "входом" в 4 секунды
    '...' #Происходят некие действия по сюжету
    stop music fadeout 4 # Остановка музыки с затуханием в 3 секунды
```

### Зацикливание и разцикливание музыки

Команды `loop` и `noloop` также являются необязательными. `loop` заставляет музыку зацикливаться, в то время как `noloop` заставляет ее играть только один раз. Если ни один из них не задан, используется значение по умолчанию канала.

Пример:

```renpy{2,3}
label my_mod:
    play sound house_crush loop #Зацикливаем звук рушения дома
    play music sabaton_rock noloop #Разцикливаем музыку, заставляя её играть только один раз
```

### Поочерёдное воспроизведение

Оператор `queue` используется для создания очереди аудиофайлов. Они будут воспроизведены, когда канал закончит воспроизведение текущего воспроизводимого файла, если таковой имеется.

Команда для кортежа воспроизводимых файлов начинается с ключевого слова `queue`, за которым следует имя канала для воспроизведения звука. Он опционально принимает команды `fadein`, `loop` и `noloop`.

Пример:

```renpy
label my_mod:
    queue music ["rock.mp3", "hip_hop.mp3", "jazz.mp3"] #Поочерёдно воспроизводит три аудиофайла через канал 'music'. После окончания первого файла, сразу же начинается следующий.
```
